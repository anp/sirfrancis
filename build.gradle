plugins {
    id 'java'
    id 'application'
    id "us.kirchmeier.capsule" version "0.10.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '0.1-CAPSULE'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

//fill this out and uncomment
mainClassName = 'io.sirfrancis.bacon.BaconApplication'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    def dropwizardVersion = '0.7.1'
    def orientDBVersion = '2.0'
    compile(
            'io.dropwizard:dropwizard-core:' + dropwizardVersion,
            'io.dropwizard:dropwizard-client:' + dropwizardVersion,
            'io.dropwizard:dropwizard-auth:' + dropwizardVersion,
            'io.dropwizard:dropwizard-jdbi:' + dropwizardVersion,
            'io.dropwizard:dropwizard-views-mustache:' + dropwizardVersion,
            'com.orientechnologies:orientdb-graphdb:' + orientDBVersion,
            'com.orientechnologies:orientdb-lucene:' + orientDBVersion,
            'org.apache.lucene:lucene-core:4.7.0',
            'org.apache.lucene:lucene-analyzers-common:4.7.0',
            'org.apache.lucene:lucene-queryparser:4.7.0',
            'org.apache.lucene:lucene-misc:4.7.0',
            'org.apache.lucene:lucene-spatial:4.7.0',
            //fix the jackson classpath issue
            'com.fasterxml.jackson.core:jackson-databind:2.3.3'
    )
    testCompile (
            'junit:junit:4.11',
    )
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

task capsule(type: FatCapsule) {
    applicationClass mainClassName
    reallyExecutable
}

def execJarPath = './build/libs/sirfrancis-' + version + '-capsule.jar'

task fullBuild(type: Exec, dependsOn: capsule) {
    commandLine('chmod', '+x', execJarPath)
}

task runServer(type: Exec, dependsOn: fullBuild) {
    //MAKE SURE CONFIG PATH IS CORRECT
    commandLine(execJarPath, 'server', 'bacon-local.yml')
}

task clearDB(type: Exec, dependsOn: fullBuild) {
    commandLine('rm', '-drf', '/opt/bacon/databases/bacon')
}

task dbInit(type: Exec, dependsOn: clearDB) {
    commandLine(execJarPath, 'db', '-f', '-s', 'omdb.txt', '-t', 'tomatoes.txt', 'bacon-local.yml')
}